buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'net.researchgate:gradle-release:2.0.2'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'java-library-distribution'
apply plugin: 'net.researchgate.release'

repositories {
    mavenCentral()
}

dependencies {
    compile(
            'com.google.code.gson:gson:2.2.4',
            'redis.clients:jedis:2.6.2',
            'org.apache.commons:commons-pool2:2.2'
    )
    testCompile(
            'junit:junit:4.11',
            'org.apache.commons:commons-math:2.2',
            'com.google.guava:guava:11.0.2',
            'org.apache.commons:commons-lang3:3.0'
    )
}

group = "com.baqend"

def distFolder = file('dist')
def docFolder = file("doc")
def projectName = 'bloom-filter'

javadoc.destinationDir = docFolder
jar.baseName = projectName
jar.destinationDir = distFolder
distZip.destinationDir = distFolder
distTar.destinationDir = distFolder
distTar.compression "GZIP"

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
    destinationDir = distFolder
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
    destinationDir = distFolder
}

task dist() {
    dependsOn distZip, distTar, sourcesJar
}

task gitAdd(dependsOn: [dist, javadoc, uploadArchives]) << {
    'git add dist maven-repo'.execute().waitFor()
    'git add -f doc'.execute().waitFor()
}

task gitRemove() << {
    'git rm -r --cached doc'.execute().waitFor()
}

preTagCommit.dependsOn gitAdd
commitNewVersion.dependsOn gitRemove

clean {
    delete distFolder
    delete docFolder
}

release {
    preTagCommitMessage = "pre tag commit:"
    tagCommitMessage = "creating tag:"
    newVersionCommitMessage = "new version commit:"
}

artifacts {
    archives javadocJar, sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://$projectDir/maven-repo")
        }
    }
}


